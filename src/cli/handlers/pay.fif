// SPDX-FileCopyrightText: 2019 Serokell <https://serokell.io>
//
// SPDX-License-Identifier: MPL-2.0

"Address.fif" include
"Iou.fif" include

"../CliState.fif" include

{ cmdline_getarg  // channel address
  hex$>addr' { fail"Invalid address" } ifnot

  cmdline_getarg  // amount
  (number) 1 = { fail"Amount must be an integer" } ifnot
  dup 0> { fail"Amount must be positive" } ifnot

  // ( wc addr amount )

  -rot loadCliStateNice
  // ( amount cliStateNice )
  dup 2 -roll
  // ( cliStateNice amount cliStateNice )
  parseCliStateNice
  // ( cliStateNice amount cliState ourPkName theirPkName )

  drop load-key-pair drop
  // ( cliStateNice amount cliState ourSk )
  swap
  // ( cliStateNice amount ourSk cliState )

  parseCliState
  // ( cliStateNice amount ourSk wc addr globalState paymentsState )
  swap getGlobalStateShare1 swap
  // ( cliStateNice amount ourSk wc addr ourShare paymentsState )

  5 pick swap addPaymentsStateWeOwe
  // ( cliStateNice amount ourSk wc addr ourShare paymentsState' )

  dup -rot getPaymentsStateBalance >=
    { fail"Payment impossible: our commitment is not large enough" } ifnot
  // ( cliStateNice amount ourSk wc addr paymentsState' )
  4 roll swap
  // ( cliStateNice ourSk wc addr amount paymentsState' )
  dup getPaymentsStateWeOwe swap dup getPaymentsStateTheyOwe swap
  // ( cliStateNice ourSk wc addr amount weOwe theyOwe paymentsState' )
  6 -roll mkIou dup .iou!
  // ( cliStateNice paymentsState' iou )
  saveIou
  // ( cliStateNice paymentsState' )
  over getCliStateNiceCliState setCliStatePaymentsState
  swap setCliStateNiceCliState
  updateCliStateNice
} : cli_pay
