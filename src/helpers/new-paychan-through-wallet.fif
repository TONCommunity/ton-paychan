#!/usr/bin/env fift -s
"TonUtil.fif" include
"Asm.fif" include

{ ."usage: " @' $0 type ." <workchain-id> [<filename-base>]" cr
  ."Creates a new wallet in specified workchain, with private key saved to or loaded from <filename-base>.pk" cr
  ."('new-wallet.pk' by default)" cr 1 halt
} : usage
$# 1- -2 and ' usage if

$1 parse-workchain-id =: wc    // set workchain id from command line argument
def? $2 { @' $2 } { "new-wallet" } cond constant file-base

."Creating new wallet in workchain " wc . cr

"pk1" file>B constant pk1
// pk1 Blen 32 = not abort"PK1 is not 32 bytes"
"pk2" file>B constant pk2
// pk2 Blen 32 = not abort"PK2 is not 32 bytes"

25 constant share1
40 constant share2
10000000 constant timeout
10 constant fineAmount
now constant nonce

// Create new simple wallet
"build/paychan.boc" file>B B>boc
// code
<b
  <b
    pk1 B,
    pk2 B,
    share1 120 u,
    share2 120 u,
    timeout 32 u,
    fineAmount 120 i,
    nonce 64 u,
  b> // globalState
  ref,
  <b 1 4 u, b> // localState WaitingBoth
  ref,
b>
null // no libraries
// Libs{ x{ABACABADABACABA} drop x{AAAA} s>c public_lib x{1234} x{5678} |_ s>c public_lib }Libs
<b b{0011} s, 3 roll ref, rot ref, swap dict, b>  // create StateInit
dup ."StateInit: " <s csr. cr
dup hashu wc swap 2dup 2constant wallet_addr
."new wallet address = " 2dup .addr cr
2dup file-base +".addr" save-address-verbose
."Non-bounceable address (for init): " 2dup 7 .Addr cr
."Bounceable address (for later access): " 6 .Addr cr
<b
  // int_msg_info$0 ihr_disabled:Bool bounce:Bool bounced:Bool src:MsgAddress -> 011000
  b{011000} s,
  b{100} s, wallet_addr addr, // addr_std
  b{0000} s, // msg value 0 grams
  b{0} s, // extra_currencies 
  b{0000} s, // ihr_fee
  b{0000} s, // fwd_fee
  0 64 u, // created_lt
  0 32 u, // created_at
  b{11} s, swap ref, // init Just Right
  b{0} s, // body Left
  0 32 u, // reqOp
b>
// msg of simple money transfer
dup ."Internal message for initialization is " <s csr. cr
2 boc+>B dup Bx. cr
"paychan-init.boc" tuck B>file
."(Saved wallet creating query to file " type .")" cr

// ---------------------------------------
// <b b{0011} s, 3 roll ref, rot ref, swap dict, b>  // create StateInit
// dup ."StateInit: " <s csr. cr
// dup hashu wc swap 2dup 2constant wallet_addr
// ."new paychan address = " 2dup .addr cr
// 2dup file-base +".addr" save-address-verbose
// ."Non-bounceable address (for init): " 2dup 7 .Addr cr
// ."Bounceable address (for later access): " 6 .Addr cr
// <b b{1000100} s, wallet_addr addr, b{000010} s, swap <s s, b{0} s, <b 0 32 u, b> <s s, b>
// dup ."External message for initialization is " <s csr. cr
// 2 boc+>B dup Bx. cr
// "paychan-query.boc" tuck B>file
// ."(Saved wallet creating query to file " type .")" cr
